if (debugMessage.toBoolean()){
    apply plugin: 'com.android.application'
}else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def ext = rootProject.ext

android {
    compileSdkVersion ext.common.compileSdkVersion
    buildToolsVersion ext.common.buildToolsVersion
    resourcePrefix ext.prefix.message

    defaultConfig {
        if (debugMessage.toBoolean()){
            applicationId ext.message.applicationId
        }
        minSdkVersion ext.common.minSdkVersion
        targetSdkVersion ext.common.targetSdkVersion
        versionCode ext.common.versionCode
        versionName ext.common.versionName

        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    sourceSets {
        main {
            if (debugMessage.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    kapt {
        generateStubs = true
        arguments {
            arg("AROUTER_MODULE_NAME", project.getName())
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    api project(':basics:common')
    kapt ext.deps.arouterCompiler
}